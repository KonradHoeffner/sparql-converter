SELECT DISTINCT
SAMPLE(str(?corrAns)) as ?corrAns
SAMPLE(str(?responsibleFor)) as ?responsibleFor
SAMPLE(str(?responsibility)) as ?responsibility
SAMPLE(str(?distractor1)) as ?distractor1
SAMPLE(str(?distractor2)) as ?distractor2
SAMPLE(str(?distractor3)) as ?distractor3
# Ausgabe der Antworten als Literale, um ungewollte
# Nebenbezeichnungen wie Sprachinformationen zu vermeiden
{
  ?c1 rdfs:label ?corrAns.
  FILTER(LANGMATCHES(LANG(?corrAns),"en"))
  ?p rdfs:label ?responsibleFor.
  FILTER(LANGMATCHES(LANG(?responsibleFor),"en"))
  ?y rdfs:label ?responsibility.
  FILTER(LANGMATCHES(LANG(?responsibility),"en"))
  ?c2 rdfs:label ?distractor1.
  FILTER(LANGMATCHES(LANG(?distractor1),"en"))
  ?c3 rdfs:label ?distractor2.
  FILTER(LANGMATCHES(LANG(?distractor2),"en"))
  ?c4 rdfs:label ?distractor3.
  FILTER(LANGMATCHES(LANG(?distractor3),"en"))
  # Seien ?c1,?p,?y,?c2,?c3,?c4 die woertlichen Bezeichungen
  # der zugehoerigen Klassen
  {
    SELECT DISTINCT ?c1 ?p ?y ?c2 ?c3 (SAMPLE(?c4) as ?c4)
    {
      meta:Role ^meta:subTopClass ?c4.
      ?c1 (!meta:subTopClass){1,2} ?c4.
      filter(?c1!=?c4&&?c2!=?c4&&?c3!=?c4&&?c1!=?y&&?c2!=?y&&?c3!=?y&&?c4!=?y)
      # Sei ?c4 eine Klasse mit Pfadl채nge 1-2 von ?c1, ungleich ?c1, ?c2, ?c3
      {
        SELECT DISTINCT ?c1 ?p ?y ?c2 (SAMPLE(?c3) as ?c3)
        {
          meta:Role ^meta:subTopClass ?c3.
          ?c1 (!meta:subTopClass){1,2} ?c3.
          filter(?c1!=?c3&&?c2!=?c3)
          # Sei ?c3 eine Klasse mit Pfadl채nge 1-2 von ?c1, ungleich ?c1, ?c2
          {
            SELECT DISTINCT
            ?c1
            ?p
            ?y
            (SAMPLE(?c2) as ?c2)
            {
              meta:Role ^meta:subTopClass ?c2.
              ?c1 (!meta:subTopClass){1,2}
              ?c2. filter(?c1!=?c2)
              # Sei ?c2 eine Klasse mit Pfadl채nge 1-2 von ?c1, ungleich ?c1
              {
                SELECT DISTINCT 
                MIN(?c1) as ?c1 ?p ?y
                # Waehle einen minimalen Wert fuer ?c1 aus einer Gruppe von Variablen
                {
                  ?y a owl:Class.
                  # Sei ?y eine Klasse
                  ?c1 ?p ?y.
                  # Sei ?c1 ein Element, welches die Beziehung ?p zur Klasse ?y aufweist
                  ?c1 meta:subTopClass meta:Role.
                  # Sei ?c1 ein Element mit Nebenelement Rolle -> benoetigt, um Informationen wie "A ist ueber X zu B verwandt" zu erhalten
                  FILTER ( 1 >  <bif:rnd> (10, ?y) )
                  # Randomisiere die Auswahl der Klassen
                }
              }
            } GROUP BY ?c1 ?p ?y
          }
        }
      }
    }
  }
}
GROUP BY ?corrAns ?responsibleFor ?responsibility ORDER BY RAND()
#Gruppiere ueber korrekte Antwort, Responsibility und Vergleichsobjekt, um f체r jedes Tripel dieser Objekte nur eine Frage zu erhalten